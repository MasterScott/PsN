package pharmml;

use strict;
use warnings;
use File::Spec::Functions qw(devnull);
use PsN;
use nonmemrun;
use MooseX::Params::Validate;

sub is_pharmml
{
    my $filename = shift;

    open my $fh, '<', $filename;
    my $line = <$fh>;
    if ($line =~ /^\<\?xml/) {    # Magic xml descriptor.
        seek $fh, 0, 0;
        while ($line = <$fh>) {            # Check if file contains start of PharmML element
            if ($line =~ /\<PharmML/) {
                close $fh;
                return 1;
            }
        }
    }

    close $fh;
    return 0;
}

sub is_java_installed
{
    if (system('java -version >' . devnull . ' 2>&1') == 0) {
        return 1;
    } else {
        return 0;
    }
}

sub _get_classpath
{
    my $classpath = $PsN::config->{'_'}->{'converter_path'};

    return $classpath;
}

sub convert_file
{
    my $filename = shift;
    my $classpath = _get_classpath;

    my $rc = system("java -cp \"$classpath/*\" eu.ddmore.convertertoolbox.cli.Main -in $filename -out . -sn PharmML -sv 0.3.0 -tn NMTRAN -tv 7.2.0");

    return $rc;
}

sub check_converted_model
{
    my $filename = shift;
    my $ok;

    # Run nmtran to test converted file before using it with PsN

    my $ref = nonmemrun::setup_paths(nm_version => $PsN::nm_version, nmqual => 0);
    my $command = $ref->{'full_path_nmtran'} . "<$filename";

    system($command);
    unlink('FCON', 'FSIZES', 'FSTREAM', 'prsizes.f90', 'FSUBS', 'FSUBS2', 'FSUBS.f90');
    unlink('FSUBS_MU.F90', 'FLIB', 'LINK.LNK', 'FWARN', 'trash.tmp');
    if (not -e 'FREPORT') {
        $ok = 0;
    } else {
        $ok = 1
    }

    unlink('FDATA', 'FREPORT');
    return $ok;
}

sub create_minimal_pharmml
{
    # Create a minimal PharmML from a NONMEM model.
    # The intent is to use it in conjunction with an SO to
    # automatically extract types of variability parameters for example
    # This is admittedly a quick hack using neither a PharmML library nor an xml library to generate xml.
    my %parm = validated_hash(\@_,
        model => { isa => 'model' },
        filename => { isa => 'Str' },
    );
    my $model = $parm{'model'};
    my $filename = $parm{'filename'};

    eval { require so::xml; };
    if ($@) {
        die "Unable to find libxml2\n";
    }

    open my $fh, '>', $filename;

    print $fh <<'END';
<?xml version="1.0" encoding="UTF-8"?>
<PharmML xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance" 
    xmlns="http://www.pharmml.org/pharmml/0.8/PharmML"
    xsi:schemaLocation="http://www.pharmml.org/pharmml/0.8/PharmML"
    xmlns:math="http://www.pharmml.org/pharmml/0.8/Maths"
    xmlns:ct="http://www.pharmml.org/pharmml/0.8/CommonTypes"
    xmlns:ds="http://www.pharmml.org/pharmml/0.8/Dataset"
    xmlns:mdef="http://www.pharmml.org/pharmml/0.8/ModelDefinition"
    xmlns:mstep="http://www.pharmml.org/pharmml/0.8/ModellingSteps"
    xmlns:design="http://www.pharmml.org/pharmml/0.8/TrialDesign"
    writtenVersion="0.8.1">

    <ct:Name>Minimal model generated by nmoutput2so</ct:Name>
    <ModelDefinition xmlns="http://www.pharmml.org/pharmml/0.8/ModelDefinition">
        <VariabilityModel blkId="vm_err" type="residualError">
            <Level referenceLevel="false" symbId="DV"/>
        </VariabilityModel>
        <VariabilityModel blkId="vm_eta" type="parameterVariability">
            <Level referenceLevel="true" symbId="ID"/>
        </VariabilityModel>
END

    print_parameter_model(file => $fh, model => $model);

    print $fh ' ' x 4, "</ModelDefinition>\n";
    print_trial_design(file => $fh, model => $model);
    print $fh "</PharmML>";

    close $fh;
}

sub print_parameter_model
{
    my %parm = validated_hash(\@_,
        file => { isa => 'Ref' },
        model => { isa => 'model' },
    );
    my $file = $parm{'file'};
    my $model = $parm{'model'};

    print $file <<'END';
        <ParameterModel blkId="pm">
END

    print_population_parameters(file => $file, model => $model);
    print_random_variables(file => $file, model => $model);

print $file <<'END';
        </ParameterModel>
END
}

sub print_population_parameters
{
    my %parm = validated_hash(\@_,
        file => { isa => 'Ref' },
        model => { isa => 'model' },
    );
    my $file = $parm{'file'};
    my $model = $parm{'model'};

    for my $record (@{$model->problems->[0]->thetas}, @{$model->problems->[0]->omegas}, @{$model->problems->[0]->sigmas}) {
        for my $option (@{$record->options}) {
            my $name = $option->label;
            if (not defined $name) {
                $name = $option->coordinate_string;
            }
            if (not so::xml::match_symbol_idtype($name)) {
                $name = so::xml::mangle_symbol_idtype($name);
            }

            print $file '            <PopulationParameter symbId="' . $name . '"/>' . "\n"; # FIXME if no label and symbol washing
        }
    }
}

sub print_random_variables
{
    my %parm = validated_hash(\@_,
        file => { isa => 'Ref' },
        model => { isa => 'model' },
    );
    my $file = $parm{'file'};
    my $model = $parm{'model'};

    my $n = 1;
    for my $record (@{$model->problems->[0]->omegas}, @{$model->problems->[0]->sigmas}) {
        for my $option (@{$record->options}) {
            if ($option->on_diagonal) {
                my $variance = $option->init; 
                my $name = "ETA";
                if ($option->coordinate_string =~ /SIGMA/) {
                    $n = 1;
                    $name = "EPS";
                }
                print $file ' ' x 12, "<RandomVariable symbId=\"$name$n\">\n";
                print $file ' ' x 16, "<ct:VariabilityReference>\n";
                print $file ' ' x 20, "<ct:SymbRef blkIdRef=\"vm_eta\" symbIdRef=\"ID\"/>\n";
                print $file ' ' x 16, "</ct:VariabilityReference>\n";
                print $file ' ' x 16, "<Distribution>\n";
                print $file ' ' x 20, "<ProbOnto xmlns=\"http://www.pharmml.org/probonto/ProbOnto\" name=\"Normal2\">\n";   # FIXME: Does not support SD
                print $file ' ' x 24, "<Parameter name=\"mean\">\n";
                print $file ' ' x 28, "<ct:Assign>\n";
                print $file ' ' x 32, "<ct:Real>0</ct:Real>\n";
                print $file ' ' x 28, "</ct:Assign>\n";
                print $file ' ' x 24, "</Parameter>\n";
                print $file ' ' x 24, "<Parameter name=\"var\">\n";
                print $file ' ' x 28, "<ct:Assign>\n";
                print $file ' ' x 32, "<ct:Real>$variance</ct:Real>\n";
                print $file ' ' x 28, "</ct:Assign>\n";
                print $file ' ' x 24, "</Parameter>\n";
                print $file ' ' x 20, "</ProbOnto>\n";
                print $file ' ' x 16, "</Distribution>\n";
                print $file ' ' x 12, "</RandomVariable>\n";
                $n++;
            }
        }
    }
}

sub print_trial_design
{
    my %parm = validated_hash(\@_,
        file => { isa => 'Ref' },
        model => { isa => 'model' },
    );
    my $file = $parm{'file'};
    my $model = $parm{'model'};

    print $file ' ' x 4, "<design:TrialDesign>\n";
    print $file ' ' x 8, "<design:ExternalDataSet toolName=\"NONMEM\" oid=\"nm_ds\">\n";
    print $file ' ' x 8, "</design:ExternalDataSet>\n";
    print $file ' ' x 4, "</design:TrialDesign>\n";
}

=cut
            <design:ColumnMapping>
				<ColumnRef xmlns="http://www.pharmml.org/pharmml/0.8/Dataset" columnIdRef="ID"/>
				<ct:SymbRef blkIdRef="vm_mdl" symbIdRef="ID"/>
			</design:ColumnMapping>
			<design:ColumnMapping>
				<ColumnRef xmlns="http://www.pharmml.org/pharmml/0.8/Dataset" columnIdRef="TIME"/>
				<ct:SymbRef  symbIdRef="T"/>
			</design:ColumnMapping>
			<design:ColumnMapping>
				<ColumnRef xmlns="http://www.pharmml.org/pharmml/0.8/Dataset" columnIdRef="AMT"/>
				<Piecewise xmlns="http://www.pharmml.org/pharmml/0.8/Dataset">
					<math:Piece>
						<ct:SymbRef blkIdRef="sm" symbIdRef="GUT"/>
						<math:Condition>
							<math:LogicBinop op="gt">
								<ColumnRef columnIdRef="AMT"/>
								<ct:Int>0</ct:Int>
							</math:LogicBinop>
						</math:Condition>
					</math:Piece>
				</Piecewise>
			</design:ColumnMapping>
			<design:ColumnMapping>
				<ColumnRef xmlns="http://www.pharmml.org/pharmml/0.8/Dataset" columnIdRef="DV"/>
				<ct:SymbRef blkIdRef="om1" symbIdRef="Y"/>
			</design:ColumnMapping>
			<design:ColumnMapping>
				<ColumnRef xmlns="http://www.pharmml.org/pharmml/0.8/Dataset" columnIdRef="logtWT"/>
				<ct:SymbRef blkIdRef="cm" symbIdRef="logtWT"/>
			</design:ColumnMapping>

            <DataSet xmlns="http://www.pharmml.org/pharmml/0.8/Dataset">
				<Definition>
					<Column columnId="ID" columnType="id" valueType="int" columnNum="1"/>
					<Column columnId="TIME" columnType="idv" valueType="real" columnNum="2"/>
					<Column columnId="WT" columnType="undefined" valueType="real" columnNum="3"/>
					<Column columnId="AMT" columnType="dose" valueType="real" columnNum="4"/>
					<Column columnId="DVID" columnType="dvid" valueType="int" columnNum="5"/>
					<Column columnId="DV" columnType="dv" valueType="real" columnNum="6"/>
					<Column columnId="MDV" columnType="mdv" valueType="int" columnNum="7"/>
					<Column columnId="logtWT" columnType="covariate" valueType="real" columnNum="8"/>
				</Definition>
				<ExternalFile oid="id">
					<path>warfarin_conc.csv</path>
					<format>CSV</format>
					<delimiter>COMMA</delimiter>
				</ExternalFile>
			</DataSet>
=cut

# Correlation between ETAs in correlation block
# Dataset connection to id and dv columns
# Filter out SAME and FIX?

1;
