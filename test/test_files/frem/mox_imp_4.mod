;; 1. Based on:
;; 2. Description: FREM_BASE
;; x1. Author: Vijay Ivaturi
;; Modified by Joakim Nyberg for simulation using a full model approach with the covariates on the EXP-scale
;; Updated with the estimate values from the moxonidine dataset
$PROBLEM    MOXONIDINE PK
;; Full Random Effects Model
$INPUT      ID VISI AGE SEX NYHA WT ACE DIG TAD TIME CLCR AMT SS II DV
            SORT TMP TMP1 TMP2 COV1 COV2 COV3 MDV FREMTYPE
$DATA      ../frem_dataset.dta IGNORE=@

;Only keep observations
$SUBROUTINE ADVAN2 TRANS1
$PK  
;;;FREM CODE BEGIN
     BSV_AGE = ETA(4)*7.00975296545
     BSV_WT = ETA(5)*15.4784069297
     BSV_SEX = ETA(6)*0.404756978659
     BSV_ACE = ETA(7)*0.484678238821
     TV_AGE=THETA(6)
     TV_WT=THETA(7)
     TV_SEX=THETA(8)
     TV_ACE=THETA(9)
;;;FREM CODE END

TVCL  = THETA(1)
TVV   = THETA(2)
TVKA  = THETA(3)

CL    = TVCL*EXP(ETA(1))
V     = TVV*EXP(ETA(2))
KA    = TVKA*EXP(ETA(3))
ALAG1 = THETA(4)
K     = CL/V
S2    = V

$ERROR     
WA     = THETA(5)
W      = WA
IPRED1 = LOG(.025)
IF(F.GT.0.AND.TMP.EQ.0) IPRED1 = LOG(F)
IRES  = IPRED1-DV
IWRES = IRES/W
IF(TMP.EQ.0) IPRED=IPRED1

Y0= IPRED

IF(TMP.EQ.0) Y= Y0+EPS(1)*W

;;;FREM CODE BEGIN
     Y_AGE = TV_AGE + BSV_AGE
     Y_WT = TV_WT + BSV_WT
     Y_SEX = TV_SEX + BSV_SEX
     Y_ACE = TV_ACE + BSV_ACE
     IF (FREMTYPE.EQ.100) THEN
        Y = Y_AGE+EPS(2)
        IPRED = Y_AGE
     END IF
     IF (FREMTYPE.EQ.200) THEN
        Y = Y_WT+EPS(2)
        IPRED = Y_WT
     END IF
     IF (FREMTYPE.EQ.300) THEN
        Y = Y_SEX+EPS(2)
        IPRED = Y_SEX
     END IF
     IF (FREMTYPE.EQ.400) THEN
        Y = Y_ACE+EPS(2)
        IPRED = Y_ACE
     END IF
;;;FREM CODE END
$THETA  (0,24.9042) ; TVCL
 (0,99.8357) ; TVV
 (0,4.09157) ; TVKA
 0.217 FIX ; LAG
 (0,0.310105) ; RES ERR
$THETA  65.4054054054 FIX ; TV_AGE
 79.4054054054 FIX ; TV_WT
 1.2027027027 FIX ; TV_SEX
 0.635135135135 FIX ; TV_ACE
$OMEGA  BLOCK(7)
 0.0912537
 0.0635499 0.0451813
 0.353988 0.214397 2.48931
 -0.097068 -0.0763277 -0.0732549 0.9762  ;    BSV_AGE
 0.146377 0.105295 0.451207 0.231602 1.05803  ;     BSV_WT
 -0.0970211 -0.0685941 -0.340666 -0.410061 -0.74173 1  ;    BSV_SEX
 -0.00222668 0.00312881 -0.171222 -0.254595 -0.168929 0.102854 0.99999  ;    BSV_ACE
$SIGMA  1  FIX
$SIGMA  1e-07  FIX  ;     EPSCOV
$ESTIMATION METHOD=IMP RANMETHOD=3P INTER NITER=300 CTYPE=3 FNLETA=1
            ISAMPLE=1000 MCETA=2000 PRINT=1 NOABORT
$ESTIMATION METHOD=IMP INTER EONLY=1 FNLETA=1 NITER=5 ISAMPLE=3000
            MCETA=2000


