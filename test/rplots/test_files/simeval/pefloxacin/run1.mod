;THIS IS A NMTRAN RECORD FOR THE PEFLOXACIN PK ANALYSIS
;NMTRAN RECORD
$PROBLEM    pefloxacin data intraindividual modeling
$INPUT      ID OCC DV TIME AMT RATE ADDL II WT AGE CLCR SEX CEN BIL
            SBP
$DATA       pefdata.csv IGN=@
$SUBROUTINE ADVAN=ADVAN1
$PK

    OCC1=0
    OCC2=0
    OCC3=0
    IF (OCC.EQ.0) OCC1=1
    IF (OCC.EQ.1) OCC2=1
    IF (OCC.EQ.2) OCC3=1

 IF(NEWIND.NE.2) BCLCR=CLCR
 IF(NEWIND.NE.2) BWT  =WT
                 DCLCR=CLCR-BCLCR
                 DWT  =WT-BWT

    TVCL=THETA(1)*(1+THETA(4)*(CLCR-100))
    CL=TVCL*EXP(ETA(1)+OCC1*ETA(3)+OCC2*ETA(5)+OCC3*ETA(7))
    TVV=THETA(2)*(WT/70)
    V =TVV *EXP(ETA(2)+OCC1*ETA(4)+OCC2*ETA(6)+OCC3*ETA(8))
    S1=V
    K=CL/V
$ERROR
 
    IPRED=F
    W    =THETA(3)*IPRED
    IWRES=(DV-IPRED)/W
    Y=IPRED + W*ERR(1)
$THETA  (0,4.5998)       ; 1 TVCL
$THETA  (0,52.9561)      ; 2 TVV
$THETA  (0,1.6579)       ; 3 RV
$THETA  (0,0.0013,.0099) ; 4 CRCL on CL
$OMEGA  BLOCK(2)
 0.1881
 0.1065 0.1624
$OMEGA  BLOCK(2)
 0.0310
 -0.0120 0.0139
$OMEGA  BLOCK(2) SAME

$OMEGA  BLOCK(2) SAME

$SIGMA  1  FIX
$ESTIMATION SIG=3 MAX=3000 PRINT=10 MSFO=msf METH=1 INTER
$COVARIANCE
$TABLE      ID TIME IPRED IWRES CWRES NOPRINT FILE=sdtab1
$TABLE      ID ETA1 ETA2 ETA3 ETA4 ETA5 ETA6 ETA7 NOPRINT FILE=patab1
$TABLE      ID CLCR WT NOPRINT FILE=cotab1
