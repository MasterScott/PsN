#!/usr/bin/perl

use FindBin qw($Bin);
use lib "$Bin/../lib";

# Don't edit the line below, it must look exactly like this.
# Everything above this line will be replaced #

use PsN;
use strict;
use Getopt::Long;
use common_options;
use Cwd;
use OSspecific;
use ui;
# More PsN dependencies included with require further down

my $cmd_line = $0 . " " . join( " ", @ARGV );

## Configure the command line parsing
Getopt::Long::config("auto_abbrev");

my %options;
## Declare the options

my %required_options = ('covariates:s');
my %optional_options = ( 'dv:s' => undef,
						 'vpc!' => undef,
						 'check!' => undef,
						 'skip_etas:i' => undef);

my $res = GetOptions( \%options,
		      @common_options::get_opt_strings,
		      keys(%required_options),
		      keys(%optional_options) );
exit unless $res;

common_options::setup( \%options, 'frem' ); #get defaults, 
#calls set_globals etc, initiates random sequence

my %help_text;

$help_text{Pre_help_message} = <<'EOF';
    Full Random Effects Model
EOF

$help_text{Description} = <<'EOF';      
The frem program is an aid to the method described in 
A full model approach based on the covariance matrix of parameters and covariates, 
PAGE 21 (2012) Abstr 2455, M Karlsson. 

The program step by step builds a model with full random effects. 
If option -vpc is set then script will prepare model that can be run with vpc.

The main run directory will contain the frem_vpc model and frem_vpc
dataset, if option -vpc was set. The m1 subdirectory of the frem run 
directory will contain all other generated models, lst-files for 
the estimated models, and dataset 2 (the frem dataset).
EOF

$help_text{Examples} = <<'EOF';      
frem run1.mod -covariates=WT,SEX,DGRP -vpc -no-check
EOF
$help_text{Options} = <<'EOF';      
	A model file is required as argument.

      The following options are valid:
EOF

$help_text{-h} = <<'EOF';
      -h | -?
      
      With -h or -? frem will print a list of options and exit.
EOF
      
$help_text{-help} = <<'EOF';      
      -help
      
      With -help frem will print this, longer, help message.
EOF

$help_text{-covariates} = <<'EOF';
      -covariates=list

A comma-separated list of covariates, required.
Names used in $INPUT.
EOF

$help_text{-dv} = <<'EOF';
      -dv=column

      Default is DV. The name of the dependent variable. Name
used in $INPUT.
EOF
$help_text{-skip_etas} = <<'EOF';
      -skip_etas=x

      The number of input model leading ETAs to exclude from correlation with covariates.
The default is 0, which means
all model ETAs will be correlated to covariates.
Not allowed if restarting an old run.
EOF

$help_text{-vpc} = <<'EOF';
      -vpc

      Default not set. If set then script will create a frem model that
can be run with the vpc script (in a separate call to the vpc
script).
EOF

$help_text{-check} = <<'EOF';
      -check

      Set by default, disable with -no-check. Run safety check after
data set 2 generation.
EOF

$help_text{Post_help_message} = <<'EOF';
    Also see 'psn_options -h' for a description of common PsN options.
EOF

common_options::online_help( 'frem', \%options, \%help_text, \%required_options, \%optional_options);

## Check that we do have a model file
if ( scalar(@ARGV) < 1 ) {
  print "An input model file must be specified. Use 'frem -h' for help.\n";
  die;
}

if( scalar(@ARGV) > 1 ){
  print "FREM can only handle one modelfile, you listed: ",join(',',@ARGV),". Use 'frem -h' for help.\n";
  die;
}


#parse input
my @covariates=();
if (defined $options{'covariates'}){
    #comma-separated list of names
    foreach my $name (split(/,/,$options{'covariates'})){
		if (length($name)>0){
			push(@covariates,$name);
		}
    }
}


require model;
require tool::frem;

my $eval_string = common_options::model_parameters(\%options);

my $model = model -> new ( eval( $eval_string ),
			   filename                    => $ARGV[0],
			   ignore_missing_output_files => 1);
if ($model->tbs){
	die "frem is incompatible with option -tbs\n";
}

if( defined $model -> msfi_names() ){
    my @needed_files;
    foreach my $msfi_files( @{$model -> msfi_names()} ){
		#loop $PROB
		if (defined $msfi_files){
			foreach my $msfi_file( @{$msfi_files} ){
				#loop instances
				if ( defined $msfi_file ){
					my ( $dir, $file ) = OSspecific::absolute_path(cwd(),$msfi_file);
					push (@needed_files,$dir.$file) ;
				}
			}
		}
    }
    if (scalar(@needed_files)>0){
		if( defined $model -> extra_files ){
			push(@{$model -> extra_files},@needed_files);
		}else{
			$model -> extra_files(\@needed_files);
		}
    }
    
}


if ( scalar (@{$model-> problems}) > 1 ){
    print "Cannot have more than one \$PROB in the input model.\n";
    die;
}

my $est_record = $model -> record( problem_number => 1,
				   record_name => 'estimation' );
unless( scalar(@{$est_record}) > 0 ){
  print "The input model must have a \$EST record\n";
  die;
}

my $frem = 
	tool::frem->new( eval( $common_options::parameters ),
					 models	     => [ $model ],
					 top_tool           => 1,
					 covariates => \@covariates,
					 skip_etas => $options{'skip_etas'}, 
					 check => $options{'check'}, 
					 vpc => $options{'vpc'}, 
					 dv => $options{'dv'}); 

$frem-> print_options (cmd_line => $cmd_line,
		      toolname => 'frem',
		      local_options => [keys %optional_options],
		      common_options => \@common_options::tool_options);

   
$frem -> run;

if ($frem->vpc and -e $frem->directory.'frem_vpc.mod'){
	ui -> print( category => 'frem',
				 message => "The prepared frem vpc model is ".$frem->directory."frem_vpc.mod\n" );
}

#$frem -> prepare_results;
#$frem -> print_results; #only for frem_results.csv
ui -> print( category => 'frem',
	     message => "frem done\n" );

